apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.protobuf'

apply plugin: 'idea'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.gruutnetworks.gruutsigner"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 4
        versionName "chickenEgg"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        def SERVER_URL = "SERVER_URL"
        def API_VERSION = "API_VERSION"
        def API_KEY = "API_KEY"
        debug {
            buildConfigField "String", SERVER_URL, "\"http://ec2-13-209-161-44.ap-northeast-2.compute.amazonaws.com:48080/\""
            buildConfigField "String", API_VERSION, "\"v1/\""
            buildConfigField "String", API_KEY, "\"\""
            minifyEnabled false
        }
        release {
            buildConfigField "String", SERVER_URL, "\"http://ec2-13-209-161-44.ap-northeast-2.compute.amazonaws.com:48080/\""
            buildConfigField "String", API_VERSION, "\"v1/\""
            buildConfigField "String", API_KEY, "\"\""
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.16.1' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/main/proto")
    }
}

dependencies {
    def grpc_version = '1.16.1'
    def room_version = "1.1.1"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-media-compat:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    // grpc-java
    implementation "io.grpc:grpc-okhttp:$grpc_version"
    implementation "io.grpc:grpc-protobuf-lite:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    // LZ4 for compression
    implementation 'org.lz4:lz4-java:1.5.0'
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    // crypto
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    // room
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.4'
    testImplementation 'org.robolectric:robolectric:4.0.2'
    testImplementation 'androidx.test:core:1.0.0'
    testImplementation "android.arch.persistence.room:testing:$room_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:design:27.1.1'
}
